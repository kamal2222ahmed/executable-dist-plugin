plugins {
    id 'java'
    id "com.gradle.plugin-publish" version "0.9.0"
    id 'idea'
}

assert gradle.gradleVersion == '2.3'

sourceCompatibility = '1.8'

group = 'io.pivotal.labs'
version = '1.0.0'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()

    testCompile group: 'junit', name: 'junit', version: '4.12'
    def hamcrestVersion = '1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
}

pluginBundle {
    website = 'https://github.com/pivotal/executable-dist-plugin'
    vcsUrl = 'https://github.com/pivotal/executable-dist-plugin'
    description = description = file('README.adoc').readLines().first()
    tags = ['packaging', 'distribution', 'deploy', 'deployment', 'onejar', 'fatjar', 'uberjar']

    plugins {
        exedistPlugin {
            id = file("src/main/resources/META-INF/gradle-plugins").list().collect { f -> f.substring(0, f.lastIndexOf('.properties')) }.first()
            displayName = 'Executable distZip Plugin'
        }
    }
}

idea.module {
    outputDir = file('.idea/classes/main')
    testOutputDir = file('.idea/classes/test')
}
